plugins {
  id 'java-library'
  id 'scala'
  id 'maven-publish'
  id 'signing'
  id 'info.solidsoft.pitest'
  id 'io.github.cosmicsilence.scalafix'
}

description = 'Yubico internal utilities'

project.ext.publishMe = true

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
  api(platform(rootProject))

  api(
    'com.fasterxml.jackson.core:jackson-databind',
  )

  implementation(
    'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor',
    'com.fasterxml.jackson.datatype:jackson-datatype-jdk8',
    'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
    'com.google.guava:guava',
    'com.upokecenter:cbor',
    'org.slf4j:slf4j-api',
  )

  testImplementation(
    project(':yubico-util-scala'),
    'junit:junit',
    'org.scala-lang:scala-library',
    'org.scalacheck:scalacheck_2.13',
    'org.scalatest:scalatest_2.13',
  )
}


jar {
  manifest {
    attributes([
      'Implementation-Id': 'yubico-util',
      'Implementation-Title': project.description,
      'Implementation-Version': project.version,
      'Implementation-Vendor': 'Yubico',
    ])
  }
}

pitest {
  pitestVersion = '1.4.11'

  timestampedReports = false
  outputFormats = ['XML', 'HTML']

  avoidCallsTo = [
    'java.util.logging',
    'org.apache.log4j',
    'org.slf4j',
    'org.apache.commons.logging',
    'com.google.common.io.Closeables',
  ]
}
