buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
  }
}
plugins {
  id 'com.github.kt3k.coveralls' version '2.8.1'
  id 'io.codearte.nexus-staging' version '0.9.0'
  id 'net.researchgate.release' version '2.4.0'
}

import com.yubico.gradle.plugins.signing.GpgSigningPlugin

project.ext.publishEnabled = System.env.CI != 'true' && project.hasProperty('ossrhUsername')

if (publishEnabled) {
  nexusStaging {
    username = ossrhUsername
    password = ossrhPassword
    stagingProfileId = '6c61426e6529d'
  }
}

allprojects  {
  apply plugin: 'java'
  apply plugin: 'maven'

  if (publishEnabled) {
    apply plugin: GpgSigningPlugin
    signing {
      sign configurations.archives
    }
    signArchives.dependsOn check
  }

  task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
  }

  group = 'com.yubico'
  version = '0.19.0'

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  repositories {
    mavenLocal()

    maven { url "http://repo.maven.apache.org/maven2" }
  }
}

evaluationDependsOnChildren()

subprojects {
  task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }
  artifacts.archives packageSources

  task packageJavadoc(type: Jar) {
    classifier = 'javadoc'
    inputs.files javadoc.outputs
    from javadoc.destinationDir
  }
  artifacts.archives packageJavadoc

  dependencies {

    compileOnly 'org.projectlombok:lombok:1.16.18'

    compile(
      'org.slf4j:slf4j-api:1.7.25',
    )

    testCompile(
      [group: 'junit', name: 'junit', version:'4.12'],
      [group: 'org.mockito', name: 'mockito-core', version:'2.8.47'],
    )

    testRuntime(
      'org.slf4j:slf4j-simple:1.7.25',
    )

  }

  if (publishEnabled) {
    uploadArchives {
      repositories {
        mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }

          snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }

          pom.project {
            name project.description
            description 'Java server-side library for U2F'
            url 'https://developers.yubico.com/'

            developers {
              developer {
                id 'dain'
                name 'Dain Nilsson'
                email 'dain@yubico.com'
              }
              developer {
                id 'emil'
                name 'Emil Lundberg'
                email 'emil@yubico.com'
              }
            }

            licenses {
              license {
                name 'BSD-license'
                comments 'Revised 2-clause BSD license'
              }
            }

            scm {
              url 'scm:git:git://github.com/Yubico/java-u2flib-server.git'
              connection 'scm:git:git://github.com/Yubico/java-u2flib-server.git'
              developerConnection 'scm:git:ssh://git@github.com/Yubico/java-u2flib-server.git'
              tag 'HEAD'
            }
          }
        }
      }
    }
  }

}

configure(subprojects.findAll { it.name != 'u2flib-server-demo' }) {
  apply plugin: 'info.solidsoft.pitest'

  pitest {
    pitestVersion = '1.2.2'
    timestampedReports = false
    outputFormats = ['XML', 'HTML']

    avoidCallsTo = [
      'java.util.logging',
      'org.apache.log4j',
      'org.slf4j',
      'org.apache.commons.logging',
      'com.google.common.io.Closeables',
    ]
  }
}

task pitestMerge(type: com.yubico.gradle.pitest.tasks.PitestMergeTask)

coveralls {
  sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
}
tasks.coveralls {
  inputs.files pitestMerge.outputs.files
}
