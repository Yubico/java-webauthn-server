plugins {
  id 'java'
  id 'war'
  id 'application'
  id 'scala'
  id 'io.github.cosmicsilence.scalafix'
}

description = 'WebAuthn demo'

dependencies {
  implementation(platform(rootProject))

  implementation(
    project(':webauthn-server-attestation'),
    project(':webauthn-server-core'),
    project(':yubico-util'),

    'com.fasterxml.jackson.core:jackson-databind',
    'com.google.guava:guava',
    'com.upokecenter:cbor',
    'org.bouncycastle:bcprov-jdk15on',
    'org.slf4j:slf4j-api',
  )

  implementation(
    'org.eclipse.jetty:jetty-servlet:9.4.9.v20180320',
    'org.glassfish.jersey.containers:jersey-container-servlet-core:2.36',
    'javax.ws.rs:javax.ws.rs-api:2.1.1',
  )

  runtimeOnly(
    'ch.qos.logback:logback-classic:1.4.0',
    'org.glassfish.jersey.containers:jersey-container-servlet:2.36',
    'org.glassfish.jersey.inject:jersey-hk2:2.36',
  )

  testImplementation(
    platform(project(":test-platform")),
    project(':webauthn-server-core').sourceSets.test.output,
    project(':yubico-util-scala'),

    'junit:junit',
    'org.mockito:mockito-core',
    'org.scala-lang:scala-library',
    'org.scalacheck:scalacheck_2.13',
    'org.scalatest:scalatest_2.13',
  )

  modules {
    module('javax.servlet:servlet-api') {
      replacedBy('javax.servlet:javax.servlet-api')
    }
  }
}

mainClassName = 'demo.webauthn.EmbeddedServer'

[installDist, distZip, distTar].each { task ->
  def intoDir = (task == installDist) ? "/" : "${project.name}-${project.version}"
  task.into(intoDir) {
    from 'keystore.jks'
    from('src/main/webapp') {
      into 'src/main/webapp'
    }
  }
}
