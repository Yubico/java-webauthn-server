project.apply(plugin: "com.diffplug.spotless")
project.apply(plugin: "io.github.cosmicsilence.scalafix")

spotless {
    java {
        googleJavaFormat()
    }
    scala {
        scalafmt("2.6.3").configFile(project.rootProject.file("scalafmt.conf"))
    }
}

scalafix {
    configFile.set(project.rootProject.file("scalafix.conf"))

    if (project.name != "yubico-util-scala") {
        // yubico-util-scala is the only subproject with Scala sources in the "main" source set
        ignoreSourceSets.add("main")
    }
}

project.dependencies.scalafix("com.github.liancheng:organize-imports_2.13:0.6.0")


project.afterEvaluate {
    // These need to be in afterEvaluate due to this plugin
    // being conditionally applied for Java 11+ only
    project.tasks.spotlessApply.configure { dependsOn(project.tasks.scalafix) }
    project.tasks.spotlessCheck.configure { dependsOn(project.tasks.checkScalafix) }

    // Scalafix adds tasks in afterEvaluate, so their configuration must be deferred
    project.tasks.scalafix.finalizedBy(project.tasks.spotlessApply)
    project.tasks.checkScalafix.finalizedBy(project.tasks.spotlessCheck)
}
